apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply from: '../codeQuality/jacoco.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.mobiquity.androidunittests"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name

        testInstrumentationRunner "com.mobiquity.androidunittests.functionaltests.CalculatorFunctionalTestRunner"
    }


    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            apply from: "signing.gradle"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$versions.androidSupport"
    compile "com.android.support:gridlayout-v7:$versions.androidSupport"
    compile "com.android.support:cardview-v7:$versions.androidSupport"
    compile "com.android.support:percent:$versions.androidSupport"
    compile libraries.ui.slidingUpPanel

    compile libraries.retrofit2
    compile (libraries.retrofit2_XmlConverter) {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile libraries.okhttp3_LoggingInterceptor
    compile libraries.butterKnife
    compile libraries.timber
    compile libraries.dagger
    provided libraries.javaxAnnotations
    apt libraries.daggerCompiler

    testCompile testLibraries.junit
    testCompile testLibraries.truth
    testCompile testLibraries.assertjAndroid
    testCompile testLibraries.robolectric
    testCompile testLibraries.robolectricV4Shadows
    testCompile testLibraries.mockito
    testCompile testLibraries.powermockMockito
    testCompile testLibraries.powermockJunit

    androidTestCompile testLibraries.okhttp3_MockWebServer
    androidTestCompile androidTestLibraries.supportTestRunner
    androidTestCompile androidTestLibraries.supportTestRules
    androidTestCompile androidTestLibraries.espressoCore
    androidTestCompile androidTestLibraries.espressoIntents
    androidTestCompile (androidTestLibraries.espressoContrib) {
        exclude module: 'support-annotations'
        exclude module: 'gridlayout-v7'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }

    debugCompile devLibraries.leakCanary
    releaseCompile devLibraries.leakCanaryNoOp
    testCompile devLibraries.leakCanaryNoOp
}

apply from: '../codeQuality/espresso.gradle'

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$versions.androidSupport"
    }
}
